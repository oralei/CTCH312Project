title: Start
position: 17,7
---
<<declare $gameState = 0>> // Global game state
<<GetState YarnFunctions>>

<<declare $playerName = "NULL">>
<<GetName YarnFunctions>>

<<declare $playerAge = 0>>
<<GetAge YarnFunctions>>

Billy's Mom: Why hello there, {$playerName}! 
Billy's Mom: Thank you for coming out tonight.
Billy's Mom: It's been too long since Kevin and I had a little... time for ourselves.
Billy's Mom: I hope our little Billy here doesn't cause you too much trouble haha!
Billy's Mom: If you need anything just text us, you have my number.

Kevin: Oh, before I forget.
Kevin: Don't go into the basement.
Kevin: It's being renovated and I don't want you getting hurt.

Billy's Mom: Anyways, see you in a few hours {$playerName}!
    -> Don't you worry Mrs. Billy's Mom, we'll be fine!
    -> Mhm, see you later. 

<<setState 5>>
<<setTaskText "Meet Billy">>
<<parentsLeave YarnFunctions>>
===

title: MeetBilly
position: 262,7
---
<<GetName YarnFunctions>>
<<GetAge YarnFunctions>>

<<declare $watchingTV = false>>

<<if $watchingTV == false>>

Billy: I've never seen you before.
Billy: You look old.
    -> I'm not that old, I'm only {$playerAge}.
    -> You look like a twerp.
        Billy: *sticks his tongue out*
    Billy: What's your name anyways?
        -> {$playerName}
        -> Joe
            Billy: Joe?
            Billy: That's a boring name!
            -> Joe mama!
                Billy: *sticks his tongue out*
    Billy: I don't care.
    Billy: I'm gonna watch TV now.
    -> Okay have fun!
    -> Lazy bum.
        Billy: Did you say something?
        -> Nope.
    <<set $watchingTV = true>>
    <<setState 10>>
    <<setTaskText "Explore the house">>
    <<stop>>
    
<<else>>
    Billy: I love this show!
<<endif>>
===

title: feedBillyNode
position: 511,6
---
<<declare $hasPizza = false>>
<<GetHasPizza YarnFunctions>>

<<GetState YarnFunctions>>

<<if $gameState == 15>>
    <<setTaskText "Make food for Billy">>
    <<if $hasPizza>>
        Billy: That pizza is cold.
        Billy: I want it hot!
    <<else>>
        Billy: I'm hungry!
        Billy: I want pizza!
    <<endif>>
<<elseif $gameState == 20>>
    Billy: Mmmm... pizza... hehe...
    ...
    <color=\#68829c><i>That pizza was pretty greasy.</i></color>
    <color=\#68829c><i>I should probably wash my hands...</i></color>
    <<setState 25>>
    <<setTaskText "Go to the washroom">>

<<endif>>

<<stop>>
===

title: washroomNode
position: 754,6
---
<<GetState YarnFunctions>>
    <color=\#68829c><i>I should probably wash my hands first...</i></color>
<<if $gameState == 25>>

<<endif>>

<<stop>>
===

title: brokenVaseNode
position: 991,4
---
<<GetState YarnFunctions>>

<<if $gameState == 30>>
    Billy: Oh no!
<<elseif $gameState == 35>>
    Billy: ...
    -> Are you okay?
        Billy: Yeah... but...
    -> What did you do!?
    Billy: It wasn't me.
    Billy: It just fell over on it's own!
    -> How did that happen?
    -> You're lying.
        Billy: I'm not!
    Billy: I was watching TV and it just fell...
    -> Okay well, I'm gonna clean this up.
    <<setState 40>>
    <<setTaskText "Clean up the broken vase">>
<<endif>>

<<stop>>
===

title: letsPlayNode
position: 1225,2
---
<<GetState YarnFunctions>>

<<if $gameState == 45>>
    Billy: I'm kinda bored of the TV...
    Billy: Can we play a game?
    -> Uh...
        -> Sure?
        -> I'd be happy to!
        -> No.
            Billy: Please...
                -> No.
            Billy: Pretty please...              
            -> No.
                Billy: Pretty please....
                Billy: With a cherry on top?
                -> Fine.
            -> Fine.
    -> No.
        Billy: Please...
        -> No.
            Billy: Pretty please...              
            -> No.
                Billy: Pretty please....
                Billy: With a cherry on top?
                -> Fine.
            -> Fine.
        -> Fine.

    Billy: Yay!
    Billy: Let's play...
    Billy: Hide-and-seek!
    Billy: You count to ten, and I'll hide!
    Billy: Then you have to find me!
    -> Okay sounds good.
        -> Your mom told me not to go to the basement, okay?
        Billy: Okay!
        Billy: Now go to the corner there and count!
    <<setState 50>>
    <<setTaskText "Go to the corner">>
<<elseif $gameState == 50>>
    Billy: Go to the corner already!
<<endif>>

<<stop>>
===

title: hideAndSeekNode
position: 1462,2
---
<<GetState YarnFunctions>>
<<declare $theThing = false>>

<<if $theThing == false>>
    Billy: Remember...
    Billy: No peeking!
    <<set $theThing = true>>
<<endif>>

<<if $gameState == 55>>
    10...
    9...
    8...
    7...
    6...
    5, 4, 3, 2, 1!
    Ready or not here I come!
<<elseif $gameState == 60>>
    10...
    9...
    Uh...
    5, 4, 3, 2, 1!
    Ready or not here I come!
<<elseif $gameState == 65>>
    10...
    9...
    Uh...
    5, 4, 3, 2, 1!
    Ready or not here I come!
<<elseif $gameState == 70>>
    10...
    ...
    9...
    ...
    Ah, whatever.
    Ready or not here I come!
<<endif>>
<<setTaskText "Find Billy">>

<<stop>>
===

title: findBillyNode
position: 1697,1
---
<<GetState YarnFunctions>>

<<if $gameState == 55>>
    Billy: Aww, you found me!
    Billy: Let's go again.
    Billy: Go back to the corner and count to 10.
    <<setState 56>>
<<elseif $gameState == 56>>
    Billy: Go back to the corner already!
<<elseif $gameState == 60>>
    Billy: You're too good!
    Billy: Again, again!
    <<setState 61>>
<<elseif $gameState == 61>>
    Billy: Go back to the corner already!
<<elseif $gameState == 65>>
    Billy: ...
    Billy: Can we play one last game?
    -> Fine, we can do one more.
    Billy: But let's make it harder with the lights off this time.
    Billy: Also count slower this time!
    <<setState 66>>
<<elseif $gameState == 66>>
    Billy: Go back to the corner already!
<<endif>>

<<stop>>
===

title: topStairsNode
position: 1931,2
---
<<GetState YarnFunctions>>

<<if $gameState == 70>>
    Billy: ...
    Billy: Down...
    Billy: ...
    Billy: There...
    <<setState 75>>
    <<setTaskText "Investigate the basement">>
<<elseif $gameState == 75>>
    Billy: ...
    Billy: I...
    Billy: ...
<<endif>>
<<stop>>
===

title: noEntranceNode
position: 2153,2
---
<<GetState YarnFunctions>>

<<if $gameState == 75>>
    The door's locked and the tape is undisturbed.
    Guess there's nothing wrong down here.
<<else>>
    It's locked.
    Let's just go back upstairs.
<<endif>>
<<stop>>
===

title: InteractObject
position: 18,227
---
<<declare $interactMsg = "This is an object">>
<color=\#68829c><i>{$interactMsg}</i></color>
<<stop>>
===